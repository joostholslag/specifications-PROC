= Examples

The following examples rely on the 'Basic Patient' and extension Subject Proxies defined in the {openehr_subject_proxy}#_core_patient_proxy[openEHR Subject Proxy Specification^].

== General patient

----
dlm Basic_patient 

use 
    Basic_patient_DS

rules

    age: Duration
        Result <- {Env}.current_date() - date_of_birth
        
    bmi: Quantity
        Result <- weight / height ^ 2
----


== Breast Cancer Decision Protocol

----
dlm guideline Oncology_breast_cancer.v0.5.0

use
    Breast_cancer_patient_DS
    
input

    tnm_t: String
        currency = 60 days

    tnm_n: String
        currency = 60 days

    tnm_m: String
        currency = 60 days

    tnm_g: String
        currency = 60 days

    estrogen_receptor: Terminology_term «pos_neg_vs»
        currency = 60 days

    progesterone_receptor:  Terminology_term «pos_neg_vs»
        currency = 60 days

    her2_expression: Terminology_term «pos_neg_vs»
        currency = 60 days

    ki67: Quantity
        currency = 60 days
        ranges = {
            high:   |>= 14%|,
            normal: |< 14%|
        }

    ejection_fraction: Quantity
        currency = 60 days
        ranges = {
            normal:  |>= 40%|,
            low  :   |< 40%|
        }
                
conditions
        
    er_negative:
        estrogen_receptor = |negative|

    er_positive:
        estrogen_receptor = |positive|
        
    pr_negative:
        progesterone_receptor = |negative|

    pr_positive:
        progesterone_receptor = |positive|
        
    her2_negative:
        her2_expression = |negative|

    her2_positive:
        her2_expression = |positive|

    non_class_I_heart_failure:
        has_diagnosis ({|heart failure (class II)|, 
                    |heart failure (class III)|,
                    |heart failure (class IV)|})
                        
    anthracyclines_contraindicated:
        has_diagnosis (|Transmural MI|) or
        ejection_fraction.in_range (low) or
        non_class_I_heart_failure
                                
    taxanes_contraindicated:
        is_type1_diabetic or
        has_allergy (|taxanes|) or 
        has_intolerance (|taxanes|)
        
rules

    molecular_subtype: Terminology_term
        if er_positive and her2_negative and not ki67.in_range (high)
            Result <- |Luminal A|

        elseif er_positive and her2_negative and ki67.in_range (high)
            Result <- |Luminal B (HER2 negative)|

        elseif er_positive and her2_positive
            Result <- |Luminal B (HER2 positive)|

        elseif er_negative and pr_negative and her2_positive and ki67.in_range (high)
            Result <- |HER2|

        elseif er_negative and pr_negative and her2_negative and ki67.in_range (high)
            Result <- |Triple negative|
            
        else 
            Result <- |none|
    
    chemotherapy_regime: Terminology_term
        if not metastatic 
            if molecular_subtype in {|Luminal B (HER2 negative)|, |Triple negative|} and
                    (tnm_t > '1a' or tnm_n > '0')
                Result <- |taxanes|
            
            elseif molecular_subtype = |Luminal A| and 
                    (tnm_t >= '3' or tnm_n >= '2' or tnm_g >= '3')
                Result <- |anthracyclines|
            
            elseif molecular_subtype = |Luminal B (HER2 positive)| and 
                    (tnm_t = '1b' or tnm_t = '1c' and tnm_n = '0')
                    or
                    molecular_subtype = |HER2| and 
                    (tnm_t = '1b' and tnm_n = '0')
                Result <- |paditaxel + trastuzumab|
            else ...
                Result <-
            
        else -- metastatic
            if ...
                Result <-
            elseif ...
                Result <-
            else
                Result <-

----
