= Antenatal Care

The following pregnancy care DLM was developed by D S Alves as part of a PhD thesis at Federal University of Pernambuco (UFPE), Brazil.

== Pregnancy Risk Assessment DLM

----
dlm ruleset Obstetric_pregnancy 

preconditions

    is_pregnant
    
types

    Risk_value ::= Ordinal
        map =
            -------------------
            [emergency]:    5,
            [high_risk]:    2,
            [low_risk]:     0
            -------------------
        ;

input

    is_type1_diabetic: Boolean

    previous_obstetric_hypertension: Boolean

    previous_pre_eclampsia: Boolean

    previous_eclampsia: Boolean

    previous_gestational_diabetes: Boolean

    family_history_of_diabetes: Boolean

    race_related_diabetes_risk: Boolean
    
input -- <time_window = tw_previous_episode>

    previous_macrosomic_baby: Boolean

input -- <time_window = tw_current_episode>

    is_pregnant: Boolean

    has_gestational_diabetes: Boolean

    has_pregnancy_hypertension: Boolean 

    has_pre_eclampsia: Boolean

    has_eclampsia: Boolean
    
    has_anaemia_of_pregnancy: Boolean

    bmi: Quantity
        ranges =
            ------------------------------------
            [high]:      |>30 kg/m^2|,
            [normal]:    |>=15 .. <= 30 kg/m^2|,
            [low]:       |< 15 kg/m^2|
            ------------------------------------
        ;
    
input

    amniotic_fluid_state: Terminology_term
        currency = 24h

    uterine_fundal_height: Quantity
        currency = 24h

    vaginal_blood_flow: Quantity
        currency = 30mins
        ranges =
            [critical_high]: |> 500 mL/hr|

    |
    | Assessment of vaginal bleeding, pain, confirm
    | with ultrasound. Result value-set includes
    | placenta previa, risk of miscarriage, extopic
    | pregnancy etc
    |
    vaginal_physical_exam_assessment: Terminology_term
        currency = 48h
        
    vomiting_assessment: Terminology_term
    
    ultrasound_finding: Terminology_term

input -- <source = "manual">

    haemorrhage: Boolean

    contractions_commenced: Boolean

    braxton_hicks_contraction: Boolean
    
    amniotic_volume: Real
        ranges =
            ------------------------------------
            [polyhydramnios]:    |> 14|,
            [normal]:            |>= 5 .. <= 14|,
            [oligohydramnios]:   |< 5|
            ------------------------------------
        ;

    labour_onset_assessment: Terminology_term
    
rules

    anaemia_type: Terminology_term
        Result := not has_anaemia_of_pregnancy ? [excluded] : [anaemia_of_pregnancy]

    |
    | Return the highest level risk of any of the
    | assessed risks
    |
    effective_risk: Risk_value
        Result := Result.max ({fundal_height_related_risk, 
                    amniotic_fluid_risk,
                    vaginal_bleeding_related_risk,
                    hypertension_risk,
                    hyperemesis_related_risk,
                    gestational_diabetes_risk,
                    anaemia_risk})
        ;
        
    fundal_height_related_risk: Risk_value
        Result := case ultrasound_finding in
            =================================================
            [interuterine_growth_retardation],
            [multiple_pregnancy],
            [macrosomia]:                        [high_risk],
            -------------------------------------------------
            *:                                   [low_risk]
            =================================================
        ;
    
    amniotic_fluid_risk: Risk_value
        Result := case amniotic_fluid_state in
            ===================================
            [premature_rupture],
            [polyhydramnios]:      [emergency],
            -----------------------------------
            [oligohydramnios]:     [high_risk],
            -----------------------------------
            *:                     [low_risk]
            ===================================
        ;
    
    vaginal_bleeding_related_risk: Risk_value
        Result := case vaginal_physical_exam_assessment in
            =================================================
            [ectopic_pregnancy],
            [gestational_trophoblastic_disease]: [emergency],
            -------------------------------------------------
            [placenta_previa],
            [risk_of_miscarriage]:               [high_risk],
            -------------------------------------------------
            *:                                   [low_risk]
            =================================================
        ;
            
    hypertension_risk: Risk_value
        Result := choice of
            =================================================
            has_pre_eclampsia or 
            has_eclampsia:                      [emergency],
            -------------------------------------------------
            previous_obstetric_hypertension or
            previous_pre_eclampsia or
            previous_eclampsia or
            has_pregnancy_hypertension:         [high_risk],
            -------------------------------------------------
            *:                                  [low_risk]
            =================================================
        ;
            
    hyperemesis_related_risk: Risk_value
        Result := case vomiting_assessment in
            ========================================
            [hyperemesis_gravidarum]:   [high_risk],
            ----------------------------------------
            *:                          [low_risk]
            ========================================
        ;
            
    gestational_diabetes_risk: Risk_value
        Result := choice of
            =================================================
            bmi.in_range ([high]) or
            previous_macrosomic_baby or
            previous_gestational_diabetes or
            family_history_of_diabetes or
            race_related_diabetes_risk or
            has_gestational_diabetes or
            is_type1_diabetic:                  [high_risk],
            -------------------------------------------------
            *:                                  [low_risk]
            =================================================
        ;
            
    anaemia_risk: Risk_value
        Result := case anaemia_type in
            ============================================
            [severe_anaemia_of_pregnancy]:  [emergency],
            --------------------------------------------
            [anaemia_of_pregnancy]:         [high_risk],
            --------------------------------------------
            *:                              [low_risk]
            ============================================
        ;
    
    labour_onset_pathway: Terminology
        Result := case labour_onset_assessment in
            ====================================
            [placental_abruption],
            [premature_labour]:    [emergency],
            ------------------------------------
            [onset_of_labour],
            [labour_first_stage]:  [maternity],
            ------------------------------------
            *:                     [observation]
            ====================================
        ;
    
terminology
    term_definitions = <
        ["en"] = <
            ["low_risk"] = <
                text = <"Normal obstetric care">
                description = <"...">
            >
            ["emergency"] = <
                text = <"Obstetric emergency">
                description = <"...">
            >
            ["high_risk"] = <
                text = <"Refer to high risk care">
                description = <"...">
            >
            ["premature_rupture"] = <
                text = <"Premature rupture of membranes">
                description = <"...">
            >
            ["polyhydramnios"] = <
                text = <"polyhydramnios">
                description = <"...">
            >
            ["oligohydramnios"] = <
                text = <"oligohydramnios">
                description = <"...">
            >
            ["severe_anaemia_of_pregnancy"] = <
                text = <"anaemia of pregnancy, severe">
                description = <"...">
            >
            ["anaemia_of_pregnancy"] = <
                text = <"anaemia of pregnancy">
                description = <"...">
            >
            ["amniotic_fluid_risk"] = <
                text = <"Risk of pregnancy-related amniotic fluid">
                description = <"...">
            >
            ["hypertension_risk"] = <
                text = <"Risk of pregnancy-related hypertension">
                description = <"...">
            >
            ["diabetes_risk"] = <
                text = <"Risk of pregnancy-related diabetes">
                description = <"...">
            >
            ["anaemia_risk"] = <
                text = <"Risk of pregnancy-related anaemia">
                description = <"...">
            >
            ["previous_macrosomic_baby"] = <
                text = <"Baby weighing 4.5kg or above">
                description = <"...">
            >
            ["previous_gestational_diabetes"] = <
                text = <"xxx">
                description = <"...">
            >

            ["ectopic_pregnancy"] = <
                text = <"Ectopic pregnancy">
                description = <"...">
            >
            ["gestational_trophoblastic_disease"] = <
                text = <"Gestational trophoblastic disease">
                description = <"...">
            >
            ["previous_macrosomic_baby"] = <
                text = <"Baby weighing 4.5kg or above">
                description = <"...">
            >
            ["previous_gestational_diabetes"] = <
                text = <"xxx">
                description = <"...">
            >

        >
    >    
----
