= Antenatal Care

== Pregnancy Risk Assessment DLM

----
dlm ruleset Obstetric_pregnancy 

preconditions

    is_pregnant

input

    is_type1_diabetic: Boolean

    previous_obstetric_hypertension: Boolean

    previous_pre_eclampsia: Boolean

    previous_eclampsia: Boolean

    previous_gestational_diabetes: Boolean

    family_history_of_diabetes: Boolean

    race_related_diabetes_risk: Boolean
    
input -- <time_window = tw_previous_episode>

    previous_macrosomic_baby: Boolean

input -- <time_window = tw_current_episode>

    is_pregnant: Boolean

    has_gestational_diabetes: Boolean

    has_pregnancy_hypertension: Boolean 

    has_pre_eclampsia: Boolean

    has_eclampsia: Boolean
    
    has_anaemia_of_pregnancy: Boolean

    bmi: Quantity
        ranges =
            ------------------------------------
            [high]:      |>30 kg/m^2|,
            [normal]:    |>=15 .. <= 30 kg/m^2|,
            [low]:       |< 15 kg/m^2|
            ------------------------------------

input

    amniotic_fluid_state: Terminology_term
        currency = 24h

    uterine_fundal_height: Quantity
        currency = 24h

    vaginal_blood_flow: Quantity
        currency = 30mins
        ranges =
            [critical_high]: |> 500 mL/hr|

input -- <source = "manual">

    haemorrhage: Boolean

    contractions_commenced: Boolean

    braxton_hicks_contraction: Boolean

rules

    anaemia_type: Terminology_term
        Result := not has_anaemia_of_pregnancy ? [excluded] : [anaemia_of_pregnancy]

    risk_evaluation: Terminology_term
        ....
        
    amniotic_fluid_risk: Terminology_term
        Result :=
            case amniotic_fluid_state in [[
                =====================[ IN ]========================
                [premature rupture],
                [polyhydramnios]:           [obstetric_emergency],
                -------------------[ ELSE IN ]---------------------
                [oligohydramnios]:          [refer_high_risk_care],
                --------------------[ ELSE ]-----------------------
                *:                          [normal_obstetric_care]
                ===================================================
            ]]
    
    hypertension_risk: Terminology_term
        Result := [[
            ========================[ IF ]=============================
            has_pre_eclampsia or 
            has_eclampsia:                      [obstetric_emergency],
            ----------------------[ ELSE IF ]--------------------------
            previous_obstetric_hypertension or
            previous_pre_eclampsia or
            previous_eclampsia or
            has_pregnancy_hypertension:         [refer_high_risk_care],
            ------------------------[ ELSE ]---------------------------
            *:                                  [normal_obstetric_care]
            ===========================================================
        ]]
            
    gestational_diabetes_risk: Boolean
        Result :=
            bmi.in_range ([high]) or
            previous_macrosomic_baby or
            previous_gestational_diabetes or
            family_history_of_diabetes or
            race_related_diabetes_risk
            
    anaemia_risk: Terminology_term
        Result := 
            case anaemia_type in [[
                ======================[ IN ]===========================
                [severe_anaemia_of_pregnancy]:  [obstetric_emergency],
                --------------------[ ELSE-IN ]------------------------
                [anaemia_of_pregnancy]:         [refer_high_risk_care],
                ----------------------[ ELSE ]-------------------------
                *:                              [normal_obstetric_care]
                =======================================================
            ]]
    
terminology
    term_definitions = <
        ["en"] = <
            ["normal_obstetric_care"] = <
                text = <"Normal obstetric care">
                description = <"...">
            >
            ["obstetric_emergency"] = <
                text = <"Obstetric emergency">
                description = <"...">
            >
            ["refer_high_risk_care"] = <
                text = <"Refer to high risk care">
                description = <"...">
            >
            ["premature_rupture"] = <
                text = <"Premature rupture of membranes">
                description = <"...">
            >
            ["polyhydramnios"] = <
                text = <"polyhydramnios">
                description = <"...">
            >
            ["oligohydramnios"] = <
                text = <"oligohydramnios">
                description = <"...">
            >
            ["severe_anaemia_of_pregnancy"] = <
                text = <"anaemia of pregnancy, severe">
                description = <"...">
            >
            ["anaemia_of_pregnancy"] = <
                text = <"anaemia of pregnancy">
                description = <"...">
            >
            ["amniotic_fluid_risk"] = <
                text = <"Risk of pregnancy-related amniotic fluid">
                description = <"...">
            >
            ["hypertension_risk"] = <
                text = <"Risk of pregnancy-related hypertension">
                description = <"...">
            >
            ["diabetes_risk"] = <
                text = <"Risk of pregnancy-related diabetes">
                description = <"...">
            >
            ["anaemia_risk"] = <
                text = <"Risk of pregnancy-related anaemia">
                description = <"...">
            >
            ["previous_macrosomic_baby"] = <
                text = <"Baby weighing 4.5kg or above">
                description = <"...">
            >
            ["previous_gestational_diabetes"] = <
                text = <"xxx">
                description = <"...">
            >
        >
    >    
----
